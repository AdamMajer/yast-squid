/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/squid/dialogs.ycp
 * Package:	Configuration of squid
 * Summary:	Dialogs definitions
 * Authors:	Daniel Fiser <dfiser@suse.cz>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "squid";

import "Label";
import "Wizard";
import "SquidACL";

//import "Squid";

include "squid/helps.ycp";


/**
 * Returns a widget with setting of units
 */
term sizeUnitWidget(string id)
{
    return `ComboBox(`id(id), " ",
                [`item("B"), `item("KB"), `item("MB")]);
}
/**
 * Returns a widget with setting of units
 */
term timeUnitWidget(string id)
{
    return `ComboBox(`id(id), " ",
                [`item("seconds"), `item("minutes"), `item("hours"), `item("days")]);
}


/**
 * returns true if something added/edited otherwise false
 */
boolean AddEditHttpPortDialog(integer id_item)
{
    any ui = nil;
    boolean ret = false;
    string label = (id_item == nil ? _("Add New HTTP Port")
                                      : _("Edit Current HTTP Port"));
    term contents =
        `VBox(
            `Left(`Label(label)),
            `TextEntry(`id("host"), _("Host"), ""),
            `TextEntry(`id("port"), _("Port"), ""),
            `Frame(_("Options"),
                `VBox(
                    `CheckBox(`id("transparent"), "Transparent", false)
                )
            ),
            `VSpacing(),
            `HBox(
                `PushButton(`id(`cancel), Label::CancelButton()),
                `PushButton(`id(`ok), Label::OKButton())
            )
        );

    UI::OpenDialog(`opt(`decorated), contents);
    UI::ChangeWidget(`id("port"), `ValidChars, "1234567890");

    InitAddEditHttpPortDialog(id_item);

    while (true){
        ui = UI::UserInput();

        if (ui == `abort || ui == `cancel){
            ret = false;
            break;
        }else if (ui == `ok){
            if (StoreDataFromAddEditHttpPortDialog(id_item)){
                ret = true;
                break;
            }
        }
    }

    UI::CloseDialog();
    return ret;
}



any HttpPortsDialog(){
    any ret = nil;
    any http_port = nil;
    any tmp = nil;
    integer id_item = nil;

    term dialog_contents =
        `VBox(
            `Left(`Label( _("HTTP Ports"))),
            `Table(`id("http_port"), `header(_("Host"), _("Port"), _("Options"))),
            `HBox(
                `PushButton(`id(`add), Label::AddButton()),
                `PushButton(`id(`del), Label::DeleteButton()),
                `PushButton(`id(`edit), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - HTTP Ports Setting"), dialog_contents, "help text ad",
        Label::BackButton(), Label::NextButton());
    Wizard::DisableBackButton();

    InitHttpPortsTable();

    while (true){
        ret = UI::UserInput();

        y2debug("HttpPortsDialog(): ret == %1",ret);

        if (ret == `next) break;
        else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }else if (ret == `add){
            if (AddEditHttpPortDialog(nil))
                InitHttpPortsTable();
        }else if (ret == `edit){
            id_item = (integer)UI::QueryWidget(`id("http_port"), `CurrentItem);
            if (AddEditHttpPortDialog(id_item)){
                InitHttpPortsTable();
                UI::ChangeWidget(`id("http_port"), `CurrentItem, id_item);
            }
        }else if (ret == `del){
            DelFromHttpPortsTable((integer)UI::QueryWidget(`id("http_port"), `CurrentItem));
            InitHttpPortsTable();
        }
    }

    return ret;
}


boolean AddEditRefreshPatternDialog(integer id_item)
{
    boolean ret = false;
    any ui = nil;
    string label = (id_item == nil ? _("Add New Refresh Pattern")
                                   : _("Edit Current refresh Pattern"));
    term contents =
        `VBox(
            `Left(`Label(label)),

            `HBox(
                `TextEntry(`id("regexp"), _("Regular Expression"), ""),
                `CheckBox(`id("regexp_case_insensitive"), _("Case Insensitive"))
            ),
            `HBox(
                `Bottom(`IntField(`id("min"), _("Min"), 0, 99999, 0)),
                `Bottom(`Label(_("minutes")))
            ),
            `HBox(
                `Bottom(`IntField(`id("percent"), _("Percent"), 0, 99999, 0)),
                `Bottom(`Label("%"))
            ),
            `HBox(
                `Bottom(`IntField(`id("max"), _("Max"), 0, 99999, 0)),
                `Bottom(`Label(_("minutes")))
            ),

            `VSpacing(),

            `HBox(
                `PushButton(`id(`cancel), Label::CancelButton()),
                `PushButton(`id(`ok), Label::OKButton())
            )
        );

    UI::OpenDialog(`opt(`decorated), contents);

    InitAddEditRefreshPatternDialog(id_item);

    while (true){
        ui = UI::UserInput();

        if (ui == `cancel || ui == `abort){
            ret = false;
            break;
        }else if (ui == `ok){
            if (StoreDataFromAddEditRefreshPatternDialog(id_item)){
                ret = true;
                break;
            }
        }
    }

    UI::CloseDialog();
    return ret;
}

any CacheDialog(){
    any ret = nil;
    integer id_item = 0;
    term dialog_contents =
        `VBox(
            `Left(`Label( _("Refresh Patterns"))),
            `HBox(
                `Table(`id("refresh_patterns"), `opt(`keepSorting),
                       `header(_("Regular Expression"), _("Min"), _("Precent"), _("Max")/*, _("Options")*/)),
                `VBox(
                    `PushButton(`id(`up), Label::UpButton()),
                    `PushButton(`id(`down), Label::DownButton())
                )
            ),
            `HBox(
                `PushButton(`id(`add), Label::AddButton()),
                `PushButton(`id(`del), Label::DeleteButton()),
                `PushButton(`id(`edit), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - Cache Setting"), dialog_contents, "help",
        Label::BackButton(), Label::NextButton());

    InitRefreshPatternsTable();

    while (true){
        ret = UI::UserInput();

        y2debug("CacheDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }else if (ret == `add){
            if (AddEditRefreshPatternDialog(nil))
                InitRefreshPatternsTable();
        }else if (ret == `edit){
            id_item = (integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem);
            if (AddEditRefreshPatternDialog(id_item)){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }else if (ret == `del){
            DelFromRefreshPatternsTable((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            InitRefreshPatternsTable();
        }else if (ret == `up){
            id_item = MoveUpRefreshPattern((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            if (id_item != nil){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }else if (ret == `down){
            id_item = MoveDownRefreshPattern((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            if (id_item != nil){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }
    }

    return ret;
}

boolean CacheDirAdvancedSettingDialog(){
    boolean ret = false;
    any ui = nil;
    term contents =
        `VBox(
            `Left(`Label( _("Cache Directory Advanced Setting"))),

            //`ComboBox(`id("type"), _("Type"), []),
            `HBox(
                `IntField(`id("mbytes"), _("Size"), 1, 99999, 10),
                `Bottom(`Label(_("MB")))
            ),
            `IntField(`id("l1dirs"), _("Level 1 Directories"), 1, 99999, 20),
            `IntField(`id("l2dirs"), _("Level 2 Directories"), 1, 99999, 20),

            `VSpacing(),

            `HBox(
                `PushButton(`id(`cancel), Label::CancelButton()),
                `PushButton(`id(`ok), Label::OKButton())
            )
        );

    UI::OpenDialog(contents);


    InitCacheDirAdvancedSettingDialog();

    while (true){
        ui = UI::UserInput();

        if (ui == `cancel || ui == `abort){
            ret = false;
            break;
        }else if (ui == `ok){
            if (StoreDataFromCacheDirAdvancedSettingDialog()){
                ret = true;
                break;
            }
        }
    }


    UI::CloseDialog();
    return ret;
}

any Cache2Dialog(){
    any ret = nil;
    term dialog_contents =
        `VBox(
            `Top(`Left(
                `VBox(
                    `HBox(
                        `HSquash(`IntField(`id("cache_mem"), _("Cache Memory"), 1, 99999, 10)),
                        sizeUnitWidget("cache_mem_units"),
                        `HStretch()
                    ),
                    `VSpacing(),
                    `HBox(
                        `HSquash(`IntField(`id("cache_max_object_size"), _("Max Object Size"), 0, 99999, 0)),
                        sizeUnitWidget("cache_max_object_size_units"),
                        `HSpacing(3),
                        `HSquash(`IntField(`id("cache_min_object_size"), _("Min Object Size"), 0, 99999, 0)),
                        sizeUnitWidget("cache_min_object_size_units"),
                        `HStretch()
                    ),
                    `VSpacing(),
                    `VSquash(`HBox(
                        `HSquash(`IntField(`id("cache_swap_low"), _("Swap Low-Water Mark"), 0, 100, 0)),
                        `Bottom(`Label("%")),
                        `HSpacing(3),
                        `HSquash(`IntField(`id("cache_swap_high"), _("Swap High-Water Mark"), 0, 100, 0)),
                        `Bottom(`Label("%")),
                        `HStretch()
                    )),
                    `VSpacing(),
                    `Left(`HBox(
                        `ComboBox(`id("cache_replacement_policy"),
                                  _("Cache Replacement Policy"),
                                  [`item("lru"), `item("heap GDSF"), `item("heap LFUDA"), `item("heap LRU")]),
                        `HSpacing(3),
                        `ComboBox(`id("memory_replacement_policy"),
                                  _("Memory Replacement Policy"),
                                  [`item("lru"), `item("heap GDSF"), `item("heap LFUDA"), `item("heap LRU")])
                    )),
                    `VSpacing(),
                    `VSquash(`HBox(
                        `TextEntry(`id("cache_dir"), _("Cache Directory"), ""),
                        `Bottom(`PushButton(`id(`cache_dir), "Advanced Setting"))
                    ))
                )
            ))
        );

    Wizard::SetContentsButtons("Squid - Cache Setting 2", dialog_contents, "help",
        Label::BackButton(), Label::NextButton());

    InitCache2Dialog();

    while (true){
        ret = UI::UserInput();

        y2debug("Cache2Dialog(): ret == %1",ret);

        if (ret == `next || ret == `back){
            if (StoreDataFromCache2Dialog())
                break;
        }else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }else if (ret == `cache_dir){
            CacheDirAdvancedSettingDialog();
        }
    }

    return ret;
}



void addItemToAddEditHttpAccessDialog(boolean not, string item)
{
    list items = [];

    integer i = 0;
    foreach (term value, (list<term>)UI::QueryWidget(`id("acls"), `Items),
        {
            items = add(items, `item(`id(i), value[1]:"", value[2]:""));
            i = i+1;
        });
    items = add(items, `item(`id(i), (not == true ? "not" : ""), item));
    UI::ChangeWidget(`id("acls"), `Items, items);
}
void delItemFromAddEditHttpAccessDialog(integer id_item)
{
    list items = [];

    integer i = 0;
    foreach (term value, (list<term>)UI::QueryWidget(`id("acls"), `Items),
        {
            if (value[0]:nil != `id(id_item)){
                items = add(items, `item(`id(i), value[1]:"", value[2]:""));
                i = i+1;
            }
        });
    UI::ChangeWidget(`id("acls"), `Items, items);
}
boolean AddEditHttpAccessDialog(integer id_item)
{
    boolean ret = false;
    any ui = nil;
    string acl = "";
    boolean not = false;
    any tmp = nil;
    term contents =
        `VBox(
            `ComboBox(`id("allow_deny"), _("Allow/Deny"),
                [`item(`id("allow"), "allow"), `item(`id("deny"), "deny")]),

            //`VSpacing(),

            `MinSize(25, 7,
                `Table(`id("acls"), `header("   ", _("ACL")), [])
            ),
            `Left(`PushButton(`id(`del), Label::DeleteButton())),

            //`VSpacing(),

            `Frame( _("Add ACL"),
                `HBox(
                    `CheckBox(`id("acl_not"), _("not")),
                    `ComboBox(`id("acl"), "ACL", []),
                    `PushButton(`id(`add), Label::AddButton())
                )
            ),

            `HBox(
                `PushButton(`id(`cancel), Label::CancelButton()),
                `PushButton(`id(`ok), Label::OKButton())
            )
        );

    UI::OpenDialog(contents);

    InitAddEditHttpAccessDialog(id_item);

    while (true){
        ui = UI::UserInput();

        if (ui == `cancel || ui == `abort){
            ret = false;
            break;
        }else if (ui == `ok){
            if (StoreDataFromAddEditHttpAccessDialog(id_item)){
                ret = true;
                break;
            }
        }else if (ui == `add){
            acl = (string)UI::QueryWidget(`id("acl"), `Value);
            not = (boolean)UI::QueryWidget(`id("acl_not"), `Value);
            if (size(acl) > 0){
                addItemToAddEditHttpAccessDialog(not, acl);
                InitAddEditHttpAccessDialog(nil);
            }
        }else if (ui == `del){
            delItemFromAddEditHttpAccessDialog((integer)UI::QueryWidget(`id("acls"), `CurrentItem));
            InitAddEditHttpAccessDialog(nil);
        }
    }


    UI::CloseDialog();

    return ret;
}


boolean AddEditACLDialog(integer id_item)
{
    boolean ret = false;
    any ui = nil;
    string type = "";
    term contents =
        `VBox(
            `TextEntry(`id("name"), _("Name"), ""),
            `ComboBox(`id("type"), `opt(`notify), _("Type"), SquidACL::GetTypesToComboBox()),
            `ReplacePoint(`id(`replace_point), `Empty()),

            `HBox(
                `PushButton(`id(`cancel), Label::CancelButton()),
                `PushButton(`id(`ok), Label::OKButton())
            )
        );

    UI::OpenDialog(contents);

    InitAddEditACLDialog(id_item);

    type = (string)UI::QueryWidget(`id("type"), `Value);
    SquidACL::Replace(`replace_point, type);
    SquidACL::InitWidget(type, id_item);

    while (true){
        ui = UI::UserInput();

        if (ui == `cancel || ui == `abort){
            ret = false;
            break;
        }else if (ui == `ok){
            if (StoreDataFromAddEditACLDialog(id_item)){
                ret = true;
                break;
            }
        }else if (ui == "type"){
            type = (string)UI::QueryWidget(`id("type"), `Value);
            SquidACL::Replace(`replace_point, type);
            SquidACL::InitWidget(type, id_item);
        }
    }


    UI::CloseDialog();

    return ret;
}
any AccessControlDialog(){
    any ret = nil;
    integer id_item = nil;
    term dialog_contents =
        `VBox(
            `Left(`Label(_("ACL Groups"))),
            `Table(`id("acl"), `header(_("Name"), _("Type"), _("Description"))),
            `HBox(
                `PushButton(`id(`add_acl), Label::AddButton()),
                `PushButton(`id(`del_acl), Label::DeleteButton()),
                `PushButton(`id(`edit_acl), Label::EditButton()),
                `HStretch()
            ),

            `VSpacing(),

            `Left(`Label(_("Access Control"))),
            `HBox(
                `Table(`id("http_access"), `opt(`keepSorting), `header(_("Allow/Deny"), _("ACL Groups"))),
                `HSquash(`VBox(
                    `HWeight(1, `PushButton(`id(`up_http_access), Label::UpButton())),
                    `HWeight(1, `PushButton(`id(`down_http_access), Label::DownButton()))
                ))
            ),
            `HBox(
                `PushButton(`id(`add_http_access), Label::AddButton()),
                `PushButton(`id(`del_http_access), Label::DeleteButton()),
                `PushButton(`id(`edit_http_access), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - Access Control Setting"), dialog_contents, "help",
        Label::BackButton(), Label::NextButton());

    InitACLGroupsTable();
    InitHttpAccessTable();

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }else if (ret == `add_acl){
            if (AddEditACLDialog(nil)){
                InitACLGroupsTable();
            }
        }else if (ret == `edit_acl){
            id_item = (integer)UI::QueryWidget(`id("acl"), `CurrentItem);
            if (AddEditACLDialog(id_item)){
                InitACLGroupsTable();
                UI::ChangeWidget(`id("acl"), `CurrentItem, id_item);
            }

        }else if (ret == `add_http_access){
            if (AddEditHttpAccessDialog(nil)){
                InitHttpAccessTable();
            }
        }else if (ret == `edit_http_access){
            id_item = (integer)UI::QueryWidget(`id("http_access"), `CurrentItem);
            if (AddEditHttpAccessDialog(id_item)){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }else if (ret == `del_http_access){
            DelFromHttpAccessTable((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            InitHttpAccessTable();
        }else if (ret == `up_http_access){
            id_item = MoveUpHttpAccess((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            if (id_item != nil){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }else if (ret == `down_http_access){
            id_item = MoveDownHttpAccess((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            if (id_item != nil){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }
    }

    return ret;
}



any LoggingAndTimeoutsDialog()
{
    any ret = nil;
    term dialog_contents =
        `Top(`VBox(
            `Left(`Frame(_("Logging"),
                `VBox(
                    `TextEntry(`id("access_log"), _("Access Log") , ""),
                    `TextEntry(`id("cache_log"), _("Cache Log"), ""),
                    `TextEntry(`id("cache_store_log"), _("Cache Store Log"), ""),
                    `TextEntry(`id("cache_swap_log"), _("Cache Swap Log"), ""),
                    `Left(`CheckBox(`id("emulate_httpd_log"), _("Emulate Httpd Log?")))
                )
            )),

            `VSpacing(),

            `Left(`Frame(_("Timeouts"),
                `VBox(
                    `HBox(
                       `HSquash(`IntField(`id("connect_timeout"), _("Connection timeout"), 1, 99999, 10)),
                       timeUnitWidget("connection_timeout_units")
                    ),
                    `HBox(
                       `HSquash(`IntField(`id("client_lifetime"), _("Client Lifetime"), 1, 99999, 10)),
                       timeUnitWidget("clien_lifetime_units")
                    )
                )
            ))
        ));

    Wizard::SetContentsButtons("Squid - Logging + Timeouts Setting", dialog_contents, "help",
        Label::BackButton(), Label::NextButton());

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }
    }

    return ret;
}


any MiscellaneousDialog()
{
    any ret = nil;
    term dialog_contents =
        `Top(`VBox(
                `Left(`ComboBox(`id("error_language"),
                                 _("Language of error messages"),
                                 [])),
                 `VSpacing(),
                 `Left(`TextEntry(`id("cache_mgr"), _("Administrator's e-mail"), "")),
                 `VSpacing(),
                 `Left(`CheckBox(`id("ftp_pasive_mode"), _("Use Ftp Pasive Mode")))
        ));

    Wizard::SetContentsButtons("Squid - Miscellaneous Setting", dialog_contents, "help",
        Label::BackButton(), Label::FinishButton());

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            //if (Popup::ReallyAbort(true)) break;
            break;
            continue;
        }
    }

    return ret;
}
/* EOF */
}
/* vim: set sw=4 ts=4 et ft=ycp : */
