/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/squid/dialogs.ycp
 * Package:	Configuration of squid
 * Summary:	Dialogs definitions
 * Authors:	Daniel Fiser <dfiser@suse.cz>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "squid";

import "Label";
import "Wizard";
import "SquidACL";

include "squid/helps.ycp";
include "squid/popup_dialogs.ycp";
include "squid/inits.ycp";
include "squid/store_del.ycp";


boolean reallyAbort()
{
    if (!Squid::GetModified())
        return true;
    if (Popup::ReallyAbort(true))
        return true;
    return false;
}

/**
 * Returns a widget with setting of units
 */
term sizeUnitWidget(string id)
{
    return `ComboBox(`id(id), " ",
                [`item("B"), `item("KB"), `item("MB")]);
}
/**
 * Returns a widget with setting of units
 */
term timeUnitWidget(string id)
{
    return `ComboBox(`id(id), " ",
                [`item("seconds"), `item("minutes"), `item("hours"), `item("days")]);
}




any HttpPortsDialog(){
    any ret = nil;
    any http_port = nil;
    any tmp = nil;
    integer id_item = nil;

    term dialog_contents =
        `VBox(
            `Left(`Label( _("HTTP Ports"))),
            `Table(`id("http_port"), `opt(`notify), `header(_("Host"), _("Port"), _("Options"))),
            `HBox(
                `PushButton(`id(`add), Label::AddButton()),
                `PushButton(`id(`del), Label::DeleteButton()),
                `PushButton(`id(`edit), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - HTTP Ports Setting"), dialog_contents, HELPS["http_ports"]:"",
        Label::BackButton(), Label::NextButton());
    Wizard::DisableBackButton();

    InitHttpPortsTable();

    while (true){
        ret = UI::UserInput();

        y2debug("HttpPortsDialog(): ret == %1",ret);

        if (ret == `next) break;
        else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }else if (ret == `add){
            if (AddEditHttpPortDialog(nil))
                InitHttpPortsTable();
        }else if (ret == `edit || ret == "http_port"){
            id_item = (integer)UI::QueryWidget(`id("http_port"), `CurrentItem);
            if (AddEditHttpPortDialog(id_item)){
                InitHttpPortsTable();
                UI::ChangeWidget(`id("http_port"), `CurrentItem, id_item);
            }
        }else if (ret == `del){
            id_item = DelFromHttpPortsTable((integer)UI::QueryWidget(`id("http_port"), `CurrentItem));
            InitHttpPortsTable();
            UI::ChangeWidget(`id("http_port"), `CurrentItem, id_item);
        }
    }

    return ret;
}



any CacheDialog(){
    any ret = nil;
    integer id_item = 0;
    term dialog_contents =
        `VBox(
            `Left(`Label( _("Refresh Patterns"))),
            `HBox(
                `Table(`id("refresh_patterns"), `opt(`keepSorting, `notify),
                       `header(_("Regular Expression"), _("Min"), _("Precent"), _("Max")/*, _("Options")*/)),
                `VBox(
                    `PushButton(`id(`up), Label::UpButton()),
                    `PushButton(`id(`down), Label::DownButton())
                )
            ),
            `HBox(
                `PushButton(`id(`add), Label::AddButton()),
                `PushButton(`id(`del), Label::DeleteButton()),
                `PushButton(`id(`edit), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - Cache Setting"), dialog_contents, HELPS["cache"]:"",
        Label::BackButton(), Label::NextButton());

    InitRefreshPatternsTable();

    while (true){
        ret = UI::UserInput();

        y2debug("CacheDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }else if (ret == `add){
            if (AddEditRefreshPatternDialog(nil))
                InitRefreshPatternsTable();
        }else if (ret == `edit || ret == "refresh_patterns"){
            id_item = (integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem);
            if (AddEditRefreshPatternDialog(id_item)){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }else if (ret == `del){
            id_item = DelFromRefreshPatternsTable((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            InitRefreshPatternsTable();
            UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
        }else if (ret == `up){
            id_item = MoveUpRefreshPattern((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            if (id_item != nil){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }else if (ret == `down){
            id_item = MoveDownRefreshPattern((integer)UI::QueryWidget(`id("refresh_patterns"), `CurrentItem));
            if (id_item != nil){
                InitRefreshPatternsTable();
                UI::ChangeWidget(`id("refresh_patterns"), `CurrentItem, id_item);
            }
        }
    }

    return ret;
}


any Cache2Dialog(){
    any ret = nil;
    string cache_dir = "";

    term dialog_contents =
        `Top(`Left(`VBox(
            `VBox(
                `HBox(
                    `HWeight(1,`VBox(
                        `VWeight(1, `HBox(
                            `IntField(`id("cache_mem"), _("Cache Memory"), 1, 99999, 10),
                            sizeUnitWidget("cache_mem_units")
                        )),
                        `VSpacing(),
                        `VWeight(1, `HBox(
                            `IntField(`id("cache_max_object_size"), _("Max Object Size"), 0, 99999, 0),
                            sizeUnitWidget("cache_max_object_size_units")
                        )),
                        `VSpacing(),
                        `VWeight(1, `HBox(
                            `IntField(`id("cache_swap_low"), _("Swap Low-Water Mark"), 0, 100, 0),
                            `Bottom(`Label("%"))
                        )),
                        `VSpacing(),
                        `Left(`VWeight(1, `HBox(
                            `ComboBox(`id("cache_replacement_policy"),
                                      _("Cache Replacement Policy"),
                                      [`item("lru"), `item("heap GDSF"), `item("heap LFUDA"), `item("heap LRU")])
                        )))
                    )),
                    `HSpacing(3),
                    `HWeight(1,`VBox(
                        `VWeight(1, `HBox(`Empty())),
                        `VSpacing(),
                        `VWeight(1, `HBox(
                            `IntField(`id("cache_min_object_size"), _("Min Object Size"), 0, 99999, 0),
                            sizeUnitWidget("cache_min_object_size_units")
                        )),
                        `VSpacing(),
                        `VWeight(1, `HBox(
                            `IntField(`id("cache_swap_high"), _("Swap High-Water Mark"), 0, 100, 0),
                            `Bottom(`Label("%"))
                        )),
                        `VSpacing(),
                        `Left(`VWeight(1, `HBox(
                            `ComboBox(`id("memory_replacement_policy"),
                                      _("Memory Replacement Policy"),
                                      [`item("lru"), `item("heap GDSF"), `item("heap LFUDA"), `item("heap LRU")])
                        )))
                    ))
                ),
                `VSpacing(),
                `VSquash(`HBox(
                    `TextEntry(`id("cache_dir"), _("Cache Directory"), ""),
                    `Bottom(`PushButton(`id(`browse_cache_dir), Label::BrowseButton())),
                    `Bottom(`PushButton(`id(`cache_dir), "Advanced Setting"))
                ))
            )
        )));

    Wizard::SetContentsButtons("Squid - Cache Setting 2", dialog_contents, HELPS["cache2"]:"",
        Label::BackButton(), Label::NextButton());

    InitCache2Dialog();

    while (true){
        ret = UI::UserInput();

        y2debug("Cache2Dialog(): ret == %1",ret);

        if (ret == `next || ret == `back){
            if (StoreDataFromCache2Dialog())
                break;
        }else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }else if (ret == `cache_dir){
            CacheDirAdvancedSettingDialog();
        }else if (ret == `browse_cache_dir){
            cache_dir = UI::AskForExistingDirectory((string)UI::QueryWidget(`id("cache_dir"), `Value), "Cache Directory");
            if (cache_dir != nil){
                UI::ChangeWidget(`id("cache_dir"), `Value, cache_dir);
            }
        }
    }

    return ret;
}



any AccessControlDialog(){
    any ret = nil;
    integer id_item = nil;
    term dialog_contents =
        `VBox(
            `Left(`Label(_("ACL Groups"))),
            `Table(`id("acl"), `opt(`notify), `header(_("Name"), _("Type"), _("Description"))),
            `HBox(
                `PushButton(`id(`add_acl), Label::AddButton()),
                `PushButton(`id(`del_acl), Label::DeleteButton()),
                `PushButton(`id(`edit_acl), Label::EditButton()),
                `HStretch()
            ),

            `VSpacing(),

            `Left(`Label(_("Access Control"))),
            `HBox(
                `Table(`id("http_access"), `opt(`keepSorting, `notify), `header(_("Allow/Deny"), _("ACL Groups"))),
                `HSquash(`VBox(
                    `HWeight(1, `PushButton(`id(`up_http_access), Label::UpButton())),
                    `HWeight(1, `PushButton(`id(`down_http_access), Label::DownButton()))
                ))
            ),
            `HBox(
                `PushButton(`id(`add_http_access), Label::AddButton()),
                `PushButton(`id(`del_http_access), Label::DeleteButton()),
                `PushButton(`id(`edit_http_access), Label::EditButton()),
                `HStretch()
            )
        );

    Wizard::SetContentsButtons(_("Squid - Access Control Setting"), dialog_contents, HELPS["access_control"]:"",
        Label::BackButton(), Label::NextButton());

    InitACLGroupsTable();
    InitHttpAccessTable();

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back) break;
        else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }else if (ret == `add_acl){
            if (AddEditACLDialog(nil)){
                InitACLGroupsTable();
            }
        }else if (ret == `edit_acl || ret == "acl"){
            id_item = (integer)UI::QueryWidget(`id("acl"), `CurrentItem);
            if (AddEditACLDialog(id_item)){
                InitACLGroupsTable();
                UI::ChangeWidget(`id("acl"), `CurrentItem, id_item);
            }
        }else if (ret == `del_acl){
            id_item = DelFromACLGroupsTable((integer)UI::QueryWidget(`id("acl"), `CurrentItem));
            InitACLGroupsTable();
            UI::ChangeWidget(`id("acl"), `CurrentItem, id_item);

        }else if (ret == `add_http_access){
            if (AddEditHttpAccessDialog(nil)){
                InitHttpAccessTable();
            }
        }else if (ret == `edit_http_access || ret == "http_access"){
            id_item = (integer)UI::QueryWidget(`id("http_access"), `CurrentItem);
            if (AddEditHttpAccessDialog(id_item)){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }else if (ret == `del_http_access){
            id_item = DelFromHttpAccessTable((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            InitHttpAccessTable();
            UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
        }else if (ret == `up_http_access){
            id_item = MoveUpHttpAccess((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            if (id_item != nil){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }else if (ret == `down_http_access){
            id_item = MoveDownHttpAccess((integer)UI::QueryWidget(`id("http_access"), `CurrentItem));
            if (id_item != nil){
                InitHttpAccessTable();
                UI::ChangeWidget(`id("http_access"), `CurrentItem, id_item);
            }
        }
    }

    return ret;
}



any LoggingAndTimeoutsDialog()
{
    any ret = nil;
    string tmp = nil;
    term dialog_contents =
        `Left(`Top(`VBox(
            `Frame(_("Logging"),
                `VBox(
                    `VSquash(`HBox(
                        `TextEntry(`id("access_log"), _("Access Log") , ""),
                        `Bottom(`PushButton(`id(`access_log_browse), Label::BrowseButton()))
                    )),
                    `VSquash(`HBox(
                        `TextEntry(`id("cache_log"), _("Cache Log"), ""),
                        `Bottom(`PushButton(`id(`cache_log_browse), Label::BrowseButton()))
                    )),
                    `VSquash(`HBox(
                        `TextEntry(`id("cache_store_log"), _("Cache Store Log"), ""),
                        `Bottom(`PushButton(`id(`cache_store_log_browse), Label::BrowseButton()))
                    )),
                    `Left(`CheckBox(`id("emulate_httpd_log"), _("Emulate Httpd Log?")))
                )
            ),

            `VSpacing(),

            `Frame(_("Timeouts"),
                `VBox(
                    `HBox(
                       `IntField(`id("connect_timeout"), _("Connection timeout"), 0, 99999, 0),
                       timeUnitWidget("connect_timeout_units")
                    ),
                    `HBox(
                       `IntField(`id("client_lifetime"), _("Client Lifetime"), 0, 99999, 0),
                       timeUnitWidget("client_lifetime_units")
                    )
                )
            )
        )));

    Wizard::SetContentsButtons("Squid - Logging and Timeouts Setting", dialog_contents, HELPS["logging_timeouts"]:"",
        Label::BackButton(), Label::NextButton());

    InitLoggingFrame();
    InitTimeoutsFrame();

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back){
            if(StoreDataFromLoggingFrame() && StoreDataFromTimeoutsFrame())
                break;
        }else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }else if (ret == `access_log_browse){
            tmp = UI::AskForExistingFile("/var/log", "*", "Access Log");
            if (tmp != nil)
                UI::ChangeWidget(`id("access_log"), `Value, tmp);
        }else if (ret == `cache_log_browse){
            tmp = UI::AskForExistingFile("/var/log", "*", "Cache Log");
            if (tmp != nil)
                UI::ChangeWidget(`id("cache_log"), `Value, tmp);
        }else if (ret == `cache_store_log_browse){
            tmp = UI::AskForExistingFile("/var/log", "*", "Cache Store Log");
            if (tmp != nil)
                UI::ChangeWidget(`id("cache_store_log"), `Value, tmp);
        }
    }

    return ret;
}


any MiscellaneousDialog()
{
    any ret = nil;
    term dialog_contents =
        `Top(`VBox(
                `Left(`ComboBox(`id("error_language"),
                                 _("Language of error messages"),
                                 [])),
                 `VSpacing(),
                 `Left(`TextEntry(`id("cache_mgr"), _("Administrator's e-mail"), "")),
                 `VSpacing(),
                 `Left(`CheckBox(`id("ftp_passive"), _("Use Ftp Pasive Mode")))
        ));

    Wizard::SetContentsButtons("Squid - Miscellaneous Setting", dialog_contents, HELPS["miscellaneous"]:"",
        Label::BackButton(), Label::FinishButton());

    InitMiscellaneousDialog();

    while (true){
        ret = UI::UserInput();

        y2debug("BaseDialog(): ret == %1",ret);

        if (ret == `next || ret == `back){
            if (StoreDataFromMiscellaneousDialog())
                break;
        }else if (ret == `abort || ret == `cancel){
            if (reallyAbort()) break;
            continue;
        }
    }

    return ret;
}
/* EOF */
}
/* vim: set sw=4 ts=4 et ft=ycp : */
